PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX uom: <http://www.opengis.net/def/uom/OGC/1.0/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?feature ?label ?wkt
WHERE {
  ?feature skos:prefLabel ?label ;
           geo:hasGeometry ?geometry .
  ?geometry geo:asWKT ?wkt .


  BIND(
    geof:transform(
      "POINT(-5.016805 36.888297)"^^geo:wktLiteral,
      <http://www.opengis.net/def/crs/EPSG/0/3844>
    ) AS ?metricPoint
  )

  BIND(
    geof:buffer(
      ?metricPoint,
      500000,
      uom:metre
    ) AS ?metricBuffered
  )

  BIND(
    geof:transform(
      ?metricBuffered,
      <http://www.opengis.net/def/crs/OGC/1.3/CRS84>
    ) AS ?bufferGeom
  )

  FILTER(
    geof:sfIntersects(?wkt, ?bufferGeom)
  )
}
