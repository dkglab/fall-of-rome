PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX for: <https://dkglab.github.io/ns/for/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX pno: <http://linked.data.gov.au/def/placenames/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
  ?Municipality a pno:Place ;
        dcterms:identifier ?id ;
        pno:hasPlaceName ?Municipality_place_name_for_uri;
        skos:closeMatch ?municipality_wiki_uri , ?municipality_viaf_uri , ?municipality_geonames_uri ; 
        dcterms:description ?description ;
        rdfs:label ?Municipality_label ;
        a geo:Feature ; 
        geo:hasGeometry [ geo:asWKT ?wktLiteral ] ;
        .
  for:municipalities a geo:FeatureCollection ;
    rdfs:member ?Municipality ;
    dcterms:title "Municipalities" ;
    .
}

WHERE {
  SERVICE <x-sparql-anything:location=data/municipalities/municipalities-version2.csv> {
    fx:properties fx:csv.headers "true" .
    ?row xyz:Municipality ?Municipality_name ;
          xyz:municipality_wiki_uri ?municipality_wiki_uri ;
          xyz:wiki_id ?wiki_id ;
          xyz:municipality_viaf_uri ?municipality_viaf_uri ;
          xyz:municipality_viaf_id ?municipality_viaf_id ;
          xyz:municipality_geonames_uri ?municipality_geonames_uri ;
          xyz:municipality_geonames_id ?municipality_geonames_id ;
          xyz:description ?description ;
          .
    BIND(REPLACE(STR(?Municipality_name), "[^a-zA-Z0-9]", "_") AS ?id)
    BIND(fx:entity(for:, "place/", ?id) AS ?Municipality)  
    BIND(fx:entity(for:, "place-name/", ?id) AS ?Municipality_place_name_for_uri)  
    BIND(?Municipality_name AS ?Municipality_label) 
  }

  SERVICE <x-sparql-anything:location=data/municipalities/combined.wkt.json> {
    fx:properties
      fx:media-type "application/json" .
      
    ?feature xyz:properties/xyz:shapeName ?Municipality_name ;
             xyz:geometry ?wktLiteralString .

    BIND(STRDT(?wktLiteralString, geo:wktLiteral) AS ?wktLiteral)
  }
}