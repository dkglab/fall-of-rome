PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>
PREFIX for: <https://dkglab.github.io/ns/for/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>


CONSTRUCT {
  ?site a geo:Feature ;
    skos:prefLabel ?processed ;
    skos:altLabel ?site_name ;
    geo:hasGeometry ?geo ;
    for:hasArtifactType ?mct ;
    for:towns ?towns ;
    .

    ?geo geo:asWKT ?wkt .
}
WHERE {
  SERVICE <x-sparql-anything:> {
    fx:properties fx:csv.headers "true" .

    ?row xyz:HLLG%20%23 ?hllg_number .
    ?row xyz:Site%20Name ?site_name .
    ?row xyz:processed ?processed .
    ?row xyz:towns ?towns .
    ?row xyz:Longitude ?longitude .
    ?row xyz:Latitude ?latitude .
    ?row ?mct "1" .

    BIND(fx:entity(for:, "site/", ?hllg_number) as ?site)
    BIND(fx:entity(for:, "site/", ?hllg_number, "/geo") as ?geo)
    BIND(STRDT(
      CONCAT("POINT(", ?longitude, " ", ?latitude, ")"),
      geo:wktLiteral) as ?wkt)
  }
}
