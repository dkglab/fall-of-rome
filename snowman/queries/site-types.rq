PREFIX for: <https://dkglab.github.io/ns/for/>
PREFIX kos: <https://dkglab.github.io/ns/for/kos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wiki: <https://www.wikidata.org/entity/>
PREFIX ple: <https://pleiades.stoa.org/vocabularies/place-types/>

SELECT
  ?uri
  ?site_type
  ?id
  ?siteTypeDefinition
  ?broaderSiteType
  (GROUP_CONCAT(DISTINCT ?matchingURI; separator=", ") AS ?matchingURIs)
  (GROUP_CONCAT(CONCAT('"', ?additionalDef, '"'); separator=", ") AS ?additionalDefs)
  (GROUP_CONCAT(DISTINCT ?narrowerSiteType; separator=", ") AS ?narrowerSiteTypes)
WHERE {
  ?uri
    rdf:type skos:Concept ;
    skos:inScheme kos:site-types ;
    dcterms:identifier ?id ;
    skos:prefLabel ?site_type ;
    .

  OPTIONAL { ?uri skos:definition ?siteTypeDefinition .}
  OPTIONAL { ?uri skos:closeMatch ?matchingURI .}
  OPTIONAL { ?uri skos:note ?additionalDef .}
  OPTIONAL { ?uri skos:broader ?broaderSiteType .}
  OPTIONAL { ?narrowerSiteType skos:broader ?uri . }
}
GROUP BY ?uri ?site_type ?id ?siteTypeDefinition ?broaderSiteType 
ORDER BY LCASE(?site_type)
