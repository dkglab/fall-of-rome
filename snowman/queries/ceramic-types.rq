PREFIX for: <https://dkglab.github.io/ns/for/>
PREFIX kos: <https://dkglab.github.io/ns/for/kos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wiki: <https://www.wikidata.org/entity/>
PREFIX ple: <https://pleiades.stoa.org/vocabularies/place-types/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT
  ?uri
  ?ceramic_type
  ?id
  ?wiki_entity
  ?form_type
  ?start_date
  ?end_date
  ?suggested_dating
  (GROUP_CONCAT(DISTINCT STR(?broaderURI); separator=" | ") AS ?broader_URIs)
  (GROUP_CONCAT(DISTINCT STR(?broaderIdentifier); separator=" | ") AS ?broaderIdentifiers)
  (GROUP_CONCAT(DISTINCT STR(?broaderLabel); separator=" | ") AS ?broaderLabels)
  (GROUP_CONCAT(DISTINCT STR(?narrowerURI); separator=" | ") AS ?narrower_URIs)
  (GROUP_CONCAT(DISTINCT STR(?narrowerIdentifier); separator=" | ") AS ?narrowerIdentifiers)
  (GROUP_CONCAT(DISTINCT STR(?narrowerLabel); separator=" | ") AS ?narrowerLabels)
WHERE {
  ?uri rdf:type skos:Concept ;
       skos:inScheme kos:ceramic-types ;
       dcterms:identifier ?id ;
       skos:prefLabel ?ceramic_type .

  BIND(xsd:integer(REPLACE(?id, "^[^0-9]*([0-9]+)[a-zA-Z]?.*$", "$1")) AS ?mainNum) #used for correct ordering
  BIND(REPLACE(?id, "^.*?[0-9]+([a-zA-Z]?).*$", "$1") AS ?suffix) #used for correct ordering

  OPTIONAL { ?uri skos:exactMatch ?wiki_entity . }
  OPTIONAL { ?uri skos:definition ?form_type . }
  OPTIONAL { ?uri dcterms:temporal ?start_date . }
  OPTIONAL { ?uri dcterms:temporal ?end_date . }
  OPTIONAL { ?uri skos:note ?suggested_dating . }
  OPTIONAL { 
    ?uri skos:broader ?broaderURI .
    ?broaderURI dcterms:identifier ?broaderIdentifier . 
    ?broaderURI skos:prefLabel ?broaderLabel . 
  }
  OPTIONAL { 
    ?uri skos:narrower ?narrowerURI . 
    ?narrowerURI dcterms:identifier ?narrowerIdentifier .
    ?narrowerURI skos:prefLabel ?narrowerLabel .
  }

}
GROUP BY ?uri ?ceramic_type ?id ?wiki_entity ?form_type ?start_date ?end_date ?suggested_dating ?mainNum ?suffix
ORDER BY ?mainNum ?suffix
