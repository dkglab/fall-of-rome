PREFIX for: <https://dkglab.github.io/ns/for/>
PREFIX kos: <https://dkglab.github.io/ns/for/kos/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wiki: <https://www.wikidata.org/entity/>
PREFIX ple: <https://pleiades.stoa.org/vocabularies/place-types/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT
  ?uri
  ?ceramic_type
  ?id
  (GROUP_CONCAT(DISTINCT STR(?wiki_entity); separator=" | ") AS ?wiki_entities)
  (GROUP_CONCAT(DISTINCT STR(?form_type); separator=" | ") AS ?form_types)
  (GROUP_CONCAT(DISTINCT STR(?start_date); separator=" | ") AS ?start_dates)
  (GROUP_CONCAT(DISTINCT STR(?end_date); separator=" | ") AS ?end_dates)
  (GROUP_CONCAT(DISTINCT STR(?suggested_dating); separator=" | ") AS ?suggested_datings)
  (GROUP_CONCAT(DISTINCT STR(?ARSGroup); separator=" | ") AS ?ARSGroups)
  (GROUP_CONCAT(DISTINCT STR(?ARSGroup2); separator=" | ") AS ?ARSGroups2)
  (GROUP_CONCAT(DISTINCT STR(?ARSImportHorizon); separator=" | ") AS ?ARSImportHorizons)
  (GROUP_CONCAT(DISTINCT STR(?ARSImportHorizon2); separator=" | ") AS ?ARSImportHorizons2)
  (GROUP_CONCAT(DISTINCT STR(?broader_id); separator=" | ") AS ?broader_ids)
WHERE {
  ?uri rdf:type skos:Concept ;
       skos:inScheme kos:ceramic-types ;
       dcterms:identifier ?id ;
       skos:prefLabel ?ceramic_type .

  BIND(xsd:integer(REPLACE(?id, "^[^0-9]*([0-9]+)[a-zA-Z]?.*$", "$1")) AS ?mainNum)
  BIND(REPLACE(?id, "^.*?[0-9]+([a-zA-Z]?).*$", "$1") AS ?suffix)

  OPTIONAL { ?uri skos:exactMatch ?wiki_entity . }
  OPTIONAL { ?uri skos:definition ?form_type . }
  OPTIONAL { ?uri dcterms:temporal ?start_date . }
  OPTIONAL { ?uri dcterms:temporal ?end_date . }
  OPTIONAL { ?uri skos:note ?suggested_dating . }
  OPTIONAL { ?uri kos:ARSGroup ?ARSGroup . }
  OPTIONAL { ?uri kos:ARSGroup2 ?ARSGroup2 . }
  OPTIONAL { ?uri kos:ARSImportHorizon ?ARSImportHorizon . }
  OPTIONAL { ?uri kos:ARSImportHorizon2 ?ARSImportHorizon2 . }
  OPTIONAL {
    ?uri skos:broader ?broaderUri .
    BIND(REPLACE(STR(?broaderUri), "^.*/([^/]+)$", "$1") AS ?broader_id)
  }
}
GROUP BY ?uri ?ceramic_type ?id ?mainNum ?suffix
ORDER BY ?mainNum ?suffix
